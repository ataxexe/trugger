/*
 * Copyright 2009-2014 Marcelo Guimarães
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 *
 * You may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *           http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

configurations {
  project.group = "tools.devnull"
  project.version = "5.2.1"

  project.ext["pomFile"] = "$buildDir/${project.name}-${project.version}.pom"

  project.description = "Trugger is a project that helps you write code that " +
      "anyone can read. It is focused on deliver fluent interfaces to deal " +
      "with reflection without the verbosity of the Java Reflection API."

  project.targetCompatibility = "1.8"
  project.sourceCompatibility = "1.8"
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile group: 'org.jboss', name: 'jboss-vfs', version: '3.1.0.Final'

  testCompile group: 'tools.devnull', name: 'kodo', version: '3.0.1'
  testCompile group: 'junit', name: 'junit', version: '4.11'
  testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
}

def encoding = 'UTF-8'
def compilerArg = '-parameters'

compileJava {
  options.encoding = encoding
  options.compilerArgs << compilerArg
}

// I'm getting some errors when I try to compile test sources with -parameter
// so do not use this parameter
compileTestJava {
  options.encoding = encoding
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
  archives file(project.pomFile)
}

task pom << {
  pom {
    project {
      groupId project.group
      artifactId project.name
      packaging 'jar'

      name project.name
      description project.description

      url 'https://github.com/devnull-tools/trugger'

      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'ataxexe'
          name 'Marcelo Guimarães'
          email 'ataxexe@devnull.tools'
          organizationUrl 'http://devnull.tools'
          url 'http://www.unbelievableexception.com'
        }
      }

      scm {
        connection 'scm:git:git@github.com:devnull-tools/trugger.git'
        url 'https://github.com/devnull-tools/trugger'
      }

    }
  }.writeTo(project.pomFile)
}

task dist(type: Zip,
    dependsOn: [clean, pom, jar, sourcesJar, javadocJar, test]) {
  from "$buildDir/libs"
  from project.pomFile
  from "changelog.md"
  from "README.md"
}

signing {
  sign configurations.archives
}

