apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

configurations {
  project.group = "tools.devnull"
  project.version = "4.4"

  project.description = "Trugger is a project that helps you write code that anyone can read."

  project.targetCompatibility = "1.6"
  project.sourceCompatibility = "1.6"
}

repositories {
  mavenLocal()
  mavenCentral()
}

dependencies {
  compile group: 'org.jboss', name: 'jboss-vfs', version: '3.1.0.Final'
  testCompile group: 'junit', name: 'junit', version: '4.8'
  testCompile group: 'org.easymock', name: 'easymock', version: '2.5.2'
}

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives jar
  archives sourcesJar
  archives javadocJar
}

task pom << {
  pom {
    project {
      groupId project.group
      artifactId project.name
      packaging 'jar'

      name project.name
      description project.description

      url 'https://github.com/ataxexe/trugger'

      licenses {
        license {
          name 'The Apache Software License, Version 2.0'
          url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          distribution 'repo'
        }
      }

      developers {
        developer {
          id 'ataxexe'
          name 'Marcelo GuimarÃ£es'
          email 'ataxexe@devnull.tools'
          organizationUrl 'http://devnull.tools'
          url 'http://entra.la'
        }
      }

      scm {
        connection 'scm:git:git@github.com:ataxexe/trugger.git'
        url 'https://github.com/ataxexe/trugger'
      }

    }
  }.writeTo("${buildDir}/pom.xml")
}

task dist(type: Zip, dependsOn: [clean, jar, sourcesJar, javadocJar, test, pom]) {
  from "$buildDir/libs"
  from "$buildDir/pom.xml"
  from "changelog.txt"
}

signing {
  sign configurations.archives
}
